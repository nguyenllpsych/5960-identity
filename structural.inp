TITLE:
    Structural model;

DATA:
    FILE IS Data MPLUS.csv;

VARIABLE: 
    NAMES ARE rse_1-rse_10
              epsi_1-epsi_12
              bfas_1-bfas_100
              dids_1-dids_25
              k10_1-k10_10;
    USEVARIABLES = rse_1-rse_10
                   dids_11-dids_25
                   k10_1-k10_10;
    MISSING ALL (-999);

MODEL:
    !measurement
    breadth BY dids_11 dids_12 dids_13 dids_14 dids_15;
    depth BY dids_16 dids_17 dids_18 dids_19 dids_20;
    rumi BY dids_21 dids_22 dids_23 dids_24 dids_25;

    esteem BY rse_1-rse_10;
    distress BY k10_1-k10_10;

    !!item error covariances
    rse_3 WITH rse_4 rse_7;
    rse_4 WITH rse_7;
    rse_10 WITH rse_1 rse_3;
    rse_1 WITH rse_3;
    rse_2 WITH rse_6;

    k10_5 WITH k10_6; !both about fidgeting
    k10_2 WITH k10_3; !both about nervous

    dids_17 WITH dids_19; !both about other people
    dids_13 WITH dids_15; !both lifestyle

    !structural
    esteem ON rumi depth breadth;
    distress ON rumi depth breadth;

OUTPUT:
    SAMPSTAT STDYX MODINDICES(10);

